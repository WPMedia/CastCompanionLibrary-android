apply plugin: 'maven'

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? "${RELEASE_REPOSITORY_URL}" : ""
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? "${SNAPSHOT_REPOSITORY_URL}" : ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.groupId = "${POM_GROUP}"
                pom.artifactId = "${POM_ARTIFACT_ID}"
                pom.version = "${VERSION_NAME}"

                repository(url: getReleaseRepositoryUrl()) {
                    authentication(userName: "${artifactory_user}", password: "${artifactory_password}")
                }

                snapshotRepository(url: getSnapshotRepositoryUrl()) {
                    authentication(userName: "${artifactory_user}", password: "${artifactory_password}")
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL
                }
            }
        }
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
    }
}